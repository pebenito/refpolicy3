// SPDX-License-Identifier: GPL-2.0

domain systemd_rfkill_t inherits systemd_app_domain {
    /// Restore RF kill switch state on boot.

    //
    // Resources
    //
    @alias(systemd_rfkill_exec_t)
    extend exec  {
        file_context("/usr/lib/systemd/systemd-rfkill", file);
    }

    @alias(systemd_rfkill_var_lib_t)
    resource state inherits common_file {
        file_context("/var/lib/systemd/rfkill(/.*)?", any);
    }

    @alias(systemd_rfkill_unit_t)
    resource unit inherits systemdunit {
        /// The systemd unit.
        file_context("/usr/lib/systemd/system/systemd-rfkill.*", file);
    }

    //
    // Policy
    //
    allow(this, self, netlink_kobject_uevent_socket, manage_socket_perms);

    this.state.manage();
    init_t.state.filetrans(this, this.state, dir); var_lib_t.list(); var_t.list();

    cgroup_t.getattr_superblock();

    // Allow reading /etc/udev/udev.conf
    etc_t.read();

    fs_t.getattr_superblock();

    proc_t.getattr_superblock();

    sysctl_t.kernel.read(); sysctl_t.list(); proc_t.list();

    sysfs_t.read();

    // Allow reading /run/udev/data/+rfkill:rfkill0
    udev_t.runtime.read();

    wireless_device_t.rw();
}
