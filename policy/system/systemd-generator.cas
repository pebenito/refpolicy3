// SPDX-License-Identifier: GPL-2.0

domain systemd_generator_t inherits systemd_app_domain {
    /// Systemd unit generators

    //
    // Resources
    //
    @alias(systemd_generator_exec_t)
    extend exec  {
        file_context("/usr/lib/systemd/system-environment-generators/.*", file);
        file_context("/usr/lib/systemd/system-generators/.*", file);
        file_context("/usr/lib/systemd/user-environment-generators/.*", file);
        file_context("/usr/lib/systemd/user-generators/.*", file);
    }

    //
    // Policy
    //
    allow(this, self, capability, [ dac_override sys_admin ]);
    allow(this, self, process, setfscreate);

    bin_t.exec();

    boot_t.read();

    cloud_init_t.state.getattr();
    cloud_init_t.runtime.create();
    runtime_t.filetrans(this, cloud_init_t.runtime, dir "cloud-init");

    efivarfs_t.list();

    etc_t.read();

    etc_runtime_t.dontaudit_read();

    file_type<filesystem>.getattr_superblock();
    file_type<common_dir>.dontaudit_getattr();

    filesystem.getattr_superblock();

    fixed_disk_device_t.read(); fixed_disk_device_t<common_character_device>.read();

    fsadm_t.exec.exec();

    init_t.ps();
    init_t.runtime.create(); runtime_t.list(); var_t.list();
    init_t.runtime.read();
    init_t.runtime.rename();
    init_t.runtime.setattr();
    init_t.runtime<common_dir>.manage();
    init_t.runtime<symlink>.manage();

    init_script_t.exec.read();

    kernel_t.inherit_file_descriptors();

    kmsg_device_t.write();

    lvm_t.exec.exec();
    lvm_t.conf.mmap_read();

    mountpoint<dir>.list();

    nss.get_hosts_entry();

    proc_t.read();
    proc_t.dontaudit_getattr_superblock();

    removable_device_t.read();

    shell_exec_t.exec();

    sysctl_t.kernel.read(); sysctl_t.list();

    sysfs_t.read();
    sysfs_t<common_dir>.write();

    // Units are in /etc/systemd/system, /usr/lib/systemd/system and /run/systemd
    systemd_unit_t.getattr();

    tmpfs_t.list();

    tty_device_t.rw();

    udev_t.runtime.read();

    // Where an unlabeled mountpoint is encounted:
    dontaudit(this, unlabeled_t, dir, list_dir_perms);

    urandom_device_t.read();

    usr_t.list();

    // needed by zfs-mount-generator
    zfs_config_t.read();

    if (distro_gentoo) {
        shell_exec_t.entrypoint();
    }

    optional_policy {
        exports_t.read();

        nfsd_fs_t.rw();
    }
}